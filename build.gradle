plugins {
  id 'java'
  id 'idea'
  id 'io.quarkus'
  id 'com.adarshr.test-logger' version '2.1.0'
  id "com.commercehub.gradle.plugin.avro" version "0.21.0"
}

apply from: 'buildscripts/code-analysis.gradle'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://packages.confluent.io/maven/"
  }
}

sourceSets {
  integrationTest {
    java.srcDir file('src/integrationTest/java')
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

task integrationTest(type: Test) {
  description = 'Runs integration tests.'
  group = 'verification'

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  shouldRunAfter test
}

check.dependsOn integrationTest

configurations {
  integrationTestImplementation.extendsFrom implementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.setCanBeResolved(true)
  integrationTestRuntimeOnly.setCanBeResolved(true)
}


dependencies {
  implementation 'io.quarkus:quarkus-resteasy-jackson'

  implementation group: 'io.quarkus', name: 'quarkus-smallrye-reactive-messaging-kafka'
  implementation group: 'io.quarkus', name: 'quarkus-smallrye-reactive-messaging'
  implementation group: 'io.smallrye.reactive', name: 'smallrye-reactive-messaging-in-memory'


  implementation group: 'io.confluent', name: 'kafka-avro-serializer', version: '5.5.0'
  implementation group: 'io.confluent', name: 'kafka-streams-avro-serde', version: '5.5.0'

  implementation 'io.quarkus:quarkus-oidc'

  implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
  implementation 'io.quarkus:quarkus-resteasy'
  implementation 'io.quarkus:quarkus-mongodb-panache'
  testImplementation 'io.quarkus:quarkus-junit5'
  testImplementation 'io.rest-assured:rest-assured'
  testImplementation "org.mockito:mockito-core:2.+"

  // Integration Testing
  integrationTestImplementation 'io.quarkus:quarkus-junit5'  
  integrationTestImplementation 'org.mockito:mockito-junit-jupiter'
  integrationTestImplementation "org.testcontainers:testcontainers:1.15.2"
  integrationTestImplementation "org.testcontainers:kafka:1.15.2"

}

group 'net.explorviz'
version '1.0.0-SNAPSHOT'

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

compileTestJava {
  options.encoding = 'UTF-8'
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

test {
  systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}

integrationTest {
  systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}

idea {
  module {
    downloadJavadoc = true
  }
}
